<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge">
        <title>Kefir.js / Bacon.js tree demo</title>
        <script src="../bower_components/jquery/dist/jquery.js"></script>
        <script src="../bower_components/bacon/dist/Bacon.js"></script>
        <script src="../dist/kefir.js"></script>
        <script src="../bower_components/coffeescript/extras/coffee-script.js"></script>
        <script src="../bower_components/stats.js/build/stats.min.js"></script>
        <link rel="stylesheet" type="text/css" href="../docs-src/includes/stylesheet.css">
        <style type="text/css">
          body {
            padding:  0 1em 1em;
            max-width: 700px;
          }
        </style>
    </head>
    <body>

      <p>
        <a href="../">â†© back to Kefir.js documentation</a>
      </p>

      <h1>Kefir.js / Bacon.js tree demo</h1>

      <p>
        In this demo each branch of the tree represented by two streams
        (Top and Angle) and has a parent branch. An Angle stream depends on
        parent's Angle and on additional Oscillation stream.
        A Top stream depends on parent's Top and self Angle streams.
        Having parent's Top and child's Top we can draw a line,
        all these lines form the tree, that you can see right now.
      </p>


      <label>
        Library:
        <select id="lib">
          <option value="Kefir">Kefir</option>
          <option value="Bacon">Bacon</option>
        </select>
      </label>
      <br>
      <label>
        Depth:
        <select id="depth">
          <option value="1">1</option>
          <option value="2">2</option>
          <option value="3">3</option>
          <option value="4">4</option>
          <option value="5">5</option>
          <option value="6">6</option>
          <option value="7">7</option>
          <option value="8">8</option>
          <option value="9">9</option>
          <option value="10">10</option>
          <option value="11">11</option>
          <option value="12">12</option>
          <option value="13">13</option>
          <option value="14">14</option>
          <option value="15">15</option>
          <option value="16">16</option>
          <option value="17">17</option>
          <option value="18">18</option>
        </select>
      </label>
      <br>
      <label>
        <input type="checkbox" id="partial-render"> Do not render all branches, just calculate
      </label>

      <br>
      Total branches: <strong id="total"></strong>
      <br>



      <canvas id="canvas" width="500" height="300"></canvas>
      <script type="text/coffeescript">


          # parse URL query

          window.queryParams = do ->
            result = {}
            for param in location.search.slice(1).split('&')
              [key, value] = param.split('=')
              result[key] = value
            result

          window.getParam = (name, notFound) ->
            if (name of queryParams)
              queryParams[name]
            else
              notFound

          DEFAULT_DEPTH = 7
          DEFAULT_LIB = 'Kefir'







          # Setup selects

          $('#lib').val(getParam('lib', DEFAULT_LIB)).change ->
            location.search = "?lib=#{ $(this).val() }&depth=#{ getParam('depth', DEFAULT_DEPTH) }&partial_render=#{ getParam('partial_render', 'no') }"
          $('#depth').val(getParam('depth', DEFAULT_DEPTH)).change ->
            location.search = "?lib=#{ getParam('lib', DEFAULT_LIB) }&depth=#{ $(this).val() }&partial_render=#{ getParam('partial_render', 'no') }"
          $('#partial-render').prop('checked', getParam('partial_render') == 'yes').change ->
            val = if $(this).prop('checked') then 'yes' else 'no'
            location.search = "?lib=#{ getParam('lib', DEFAULT_LIB) }&depth=#{ getParam('depth', DEFAULT_DEPTH) }&partial_render=#{ val }"

          PARTIAL_RENDER = (getParam('partial_render') == 'yes')






          # Setup canvas

          lines = []

          canvasEl = document.getElementById('canvas')
          ctx = canvasEl.getContext('2d')
          ctx.strokeStyle = "green"


          stats = new Stats()
          document.body.appendChild( stats.domElement )

          renderLoop = ->
            stats.begin()
            stats.end()
            ctx.clearRect(0, 0, 500, 300)
            for line in lines
              if !PARTIAL_RENDER || line.width >= 1
                ctx.beginPath()
                ctx.moveTo(line.x1, line.y1)
                ctx.lineTo(line.x2, line.y2)
                ctx.lineWidth = line.width;
                ctx.stroke()
            requestAnimationFrame(renderLoop)
          renderLoop()




          # Utils

          randomize = (x, a = .1) ->
            x * (Math.random() * a * 2 + 1 - a)

          oscillationGenerator = (ticks) ->
            (frequency) ->
              min = 0
              max = Math.PI * 2
              cur = 0
              add = Math.PI / 25 * frequency
              ticks.map ->
                cur += add
                Math.sin(cur)








          # Branch represents tree branch

          class BranchKefir

            oscillation: oscillationGenerator(Kefir.interval(1000 / 25))

            constructor: (parent, addAngle) ->
              addAngle = randomize(addAngle, .4)

              @width = randomize(parent.width * 3/4)
              @length = randomize(parent.length * 3/4)

              @angle = parent.angle.sampledBy this.oscillation(.5), (parent, osc) ->
                parent + addAngle + osc * Math.PI / 100

              @top = parent.top.sampledBy @angle, (bottom, angle) =>
                x: bottom.x + Math.cos(angle) * @length
                y: bottom.y + Math.sin(angle) * @length * -1

              line = {x1: 0, y1: 0, x2: 0, y2: 0, width: @width}

              parent.top.onValue (p) ->
                line.x1 = p.x
                line.y1 = p.y

              @top.onValue (p) ->
                line.x2 = p.x
                line.y2 = p.y

              lines.push(line)


          class BranchBacon extends BranchKefir

            oscillation: oscillationGenerator(Bacon.interval(1000 / 25))






          # Setup world

          depth = getParam('depth', DEFAULT_DEPTH)
          if getParam('lib', DEFAULT_LIB) == 'Bacon'
            Lib = Bacon
            Branch = BranchBacon
          else
            Lib = Kefir
            Branch = BranchKefir


          root =
            length: 100
            width: 5
            angle: Lib.constant(Math.PI / 2)
            top: Lib.constant(x: 250, y: 300)

          addChildren = (parent, depth) ->
            if depth > 0
              addChildren(new Branch(parent, Math.PI/5), depth - 1)
              addChildren(new Branch(parent, Math.PI/-5), depth - 1)

          base = new Branch(root, 0)
          addChildren(base, depth)




          $('#total').html lines.length


        </script>
    </body>
</html>
